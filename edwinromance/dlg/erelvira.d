/////////////////////////////////////////////////////////
//                                                     //
//EDWIN ROMANCE MOD : ERELVIRA                        //
//NEW DIALOG FROM SCRATCH FOR EDWIN ROMANCE            //
/////////////////////////////////////////////////////////
BEGIN DEKARAS4
BEGIN ERELVIRA

IF ~~ THEN BEGIN ELVIRA1
  SAY @0
  IF ~~ THEN EXTERN dekaras4 DEK_IJ_ELV1
END

IF ~~ THEN BEGIN ELVIRA2
	SAY @1
  	IF ~~ THEN EXTERN BEDWIN EDW_IJ_ELV2
END

IF ~~ THEN BEGIN ELVIRA3
	SAY @2
	IF ~~ THEN EXTERN BEDWIN EDW_IJ_ELV3
END

IF ~~ THEN BEGIN ELVIRA4
	SAY @3
	IF ~~ THEN REPLY @4 GOTO ELVIRA_LOVE
	IF ~~ THEN REPLY @5 GOTO ELVIRA_BOLD
	IF ~~ THEN REPLY @6 GOTO ELVIRA_LOVE
	IF ~~ THEN REPLY @7 GOTO ELVIRA_BOLD
	IF ~
        OR(2)
        Class(Player1,MAGE_ALL) Class(Player1,BARD_ALL)
        ~ THEN REPLY @8 GOTO ELVIRA_PC_MAGE
	IF ~
        OR(3)
        Class(Player1,FIGHTER_ALL) Class(Player1,PALADIN_ALL) Class(Player1,RANGER_ALL)
        ~ THEN REPLY @9 GOTO ELVIRA_PC_FIGHTER
	IF ~
        OR(2)
        Class(Player1,CLERIC_ALL) Class(Player1, DRUID_ALL)
        ~ THEN REPLY @10 GOTO ELVIRA_PC_CLERIC
	IF ~Class(Player1, THIEF_ALL)
        ~ THEN REPLY @11 GOTO ELVIRA_PC_THIEF

END

IF ~~ THEN BEGIN ELVIRA_LOVE
	SAY @12
	IF ~~ THEN REPLY @13 GOTO ELVIRA5
	IF ~~ THEN REPLY @14 GOTO ELVIRA5
END

IF ~~ THEN BEGIN ELVIRA5
	SAY @15
	IF ~~ THEN EXTERN dekaras4 DEK_IJ_ELV2
END

IF ~~ THEN BEGIN ELVIRA_BOLD
	SAY @16
	IF ~~ THEN REPLY @17 EXTERN dekaras4 DEK_IJ_ELV3
END

IF ~~ THEN BEGIN ELVIRA_BOLD2
	SAY @18
        IF ~~ THEN GOTO ELVIRA_IMPRESSED
END

IF ~~ THEN BEGIN ELVIRA_PC_MAGE
	SAY @19
        IF ~~ THEN GOTO ELVIRA_IMPRESSED
END

IF ~~ THEN BEGIN ELVIRA_PC_FIGHTER
	SAY @20
        IF ~~ THEN GOTO ELVIRA_IMPRESSED
END

IF ~~ THEN BEGIN ELVIRA_PC_CLERIC
	SAY @21
	IF ~~ THEN EXTERN BEDWIN EddieElviraChain1
END

IF ~~ THEN BEGIN ELVIRA_PC_THIEF
	SAY@22
	IF ~~ THEN EXTERN dekaras4 DEK_IJ_ELV4
END

IF ~~ THEN BEGIN ELVIRA_PC_THIEF2
	SAY @23
        IF ~~ THEN GOTO ELVIRA_IMPRESSED
END

IF ~~ THEN BEGIN ELVIRA_IMPRESSED
	SAY @24
	IF ~~ THEN EXTERN BEDWIN EDW_IJ_ELV4
END

IF ~~ THEN BEGIN ELVIRA_IMPRESSED2
	SAY @25
	IF ~~ THEN REPLY @26 GOTO ELVIRA_PC_LOVE
	IF ~~ THEN REPLY @27 EXTERN Bedwin PcNotLoveChain1
END

IF ~~ THEN BEGIN ELVIRA_PC_LOVE
	SAY @28
	IF ~~ THEN EXTERN BEDWIN EDW_IJ_ELV5
END

IF ~~ THEN BEGIN ELVIRA_LEAVE_HAPPY
	SAY @29
	IF ~~ THEN EXTERN dekaras4 DEK_IJ_ELV5
END


/*-------------------------------------------*/

APPEND dekaras4

IF ~~ THEN BEGIN DEK_IJ_ELV1
  SAY @30
  IF ~~ THEN EXTERN BEDWIN EDW_IJ_ELV1
END

IF ~~ THEN BEGIN DEK_IJ_ELV2
	SAY @31
	IF ~~ THEN EXTERN ERELVIRA ELVIRA_IMPRESSED
END

IF ~~ THEN BEGIN DEK_IJ_ELV3
	SAY @32
	IF ~~ THEN EXTERN ERELVIRA ELVIRA_BOLD2
END

IF ~~ THEN BEGIN DEK_IJ_ELV4
	SAY @33
	IF ~~ THEN EXTERN ERELVIRA ELVIRA_PC_THIEF2
END

IF ~~ THEN BEGIN DEK_IJ_ELV5
	SAY @34
	IF ~~ THEN DO
        ~ClearAllActions()
  	StartCutSceneMode()
  	StartCutScene("ERhappyf")~ EXIT
END
END

/*-------------------------------------------*/

APPEND BEDWIN

IF ~~ THEN BEGIN EDW_IJ_ELV1
  SAY @35
  IF ~~ THEN REPLY @36 EXTERN ERELVIRA ELVIRA2
  IF ~~ THEN REPLY @37 EXTERN ERELVIRA ELVIRA2
  IF ~~ THEN REPLY @38 EXTERN ERELVIRA ELVIRA2
  IF ~~ THEN REPLY @39 EXTERN ERELVIRA ELVIRA2
END

IF ~~ THEN BEGIN EDW_IJ_ELV2
  SAY @40
  IF ~~ THEN EXTERN ERELVIRA ELVIRA3
END

IF ~~ THEN BEGIN EDW_IJ_ELV3
  SAY @41
  IF ~~ THEN EXTERN ERELVIRA ELVIRA4
END

IF ~~ THEN BEGIN EDW_IJ_ELV4
	SAY @42
	IF ~~ THEN EXTERN ERELVIRA ELVIRA_IMPRESSED2
END

IF ~~ THEN BEGIN EDW_IJ_ELV5
	SAY @43
	IF ~~ THEN EXTERN ERELVIRA ElviraDekkieChain1
END
END

/*--------------------- CHAINS! MORE CHAINS! NOSE RINGS TOO! ---------------------------*/

CHAIN BEDWIN EddieElviraChain1
  @44
== ERELVIRA
  @45
== BEDWIN
  @46
== ERELVIRA
  @47
==BEDWIN
  @48
END ERELVIRA ELVIRA_IMPRESSED

CHAIN3 BEDWIN PcNotLoveChain1
  @49
==ERELVIRA
  @50
==dekaras4
  @51
==BEDWIN
  @52
  DO ~SetGlobal("EdwinRomanceActive","GLOBAL",3)
  ClearAllActions()
  StartCutSceneMode()
  StartCutScene("ERUpsetf")~
EXIT


/*---------------------------------------*/


CHAIN ERELVIRA ElviraDekkieChain1
 @53
== dekaras4
  @54
==ERELVIRA
  @55
==dekaras4
  @56
END ERELVIRA ELVIRA_LEAVE_HAPPY
