IF
  Detect(NearestEnemyOf(Myself))
  Global("Prep","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",1)
    DisplayString(Myself,26328)
    ReallyForceSpell(Myself,WIZARD_SPELL_TURNING)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ENERGY)
    ReallyForceSpell(Myself,WIZARD_GLOBE_OF_INVULNERABILITY)
    DisplayString(Myself,25942)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
    DisplayString(Myself,39969)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    ReallyForceSpell(Myself,WIZARD_SPIRIT_ARMOR)
    ReallyForceSpell(Myself,WIZARD_SHADOW_DOOR)
    DisplayString(Myself,39968)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
    SetGlobalTimer("Spellimmunity","LOCALS",114)
    SetGlobalTimer("Nobuffsyet","LOCALS",40)
END

IF
  OR(3)
  SpellCast([PC],WIZARD_TRUE_SIGHT)
  SpellCast([PC],CLERIC_TRUE_SIGHT)
  SpellCast([PC],INQUIS_TRUE_SIGHT)
THEN
  RESPONSE #100
    SetGlobalTimer("Noillusions","LOCALS",60)
END

IF
  See(NearestEnemyOf(Myself))
  StateCheck(Myself,STATE_SILENCED)
  HaveSpell(WIZARD_VOCALIZE)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_VOCALIZE)
END

IF
  !HasImmunityEffects(Myself)
  HaveSpell(WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
  See(NearestEnemyOf(Myself))
  GlobalTimerExpired("Nobuffsyet","LOCALS")
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_SUMMON_HAKEASHAR)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_SUMMON_HAKEASHAR)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_INVISIBLE_STALKER)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_INVISIBLE_STALKER)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_ANIMATE_DEAD)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_ANIMATE_DEAD)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_SPIDER_SPAWN)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_SPIDER_SPAWN)
END

IF
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  See(NearestEnemyOf(Myself))
  OR(2)
  !GlobalTimerNotExpired("Noillusions","LOCALS")
  GlobalTimerNotExpired("Spellimmunity","LOCALS")
  HaveSpell(WIZARD_MIRROR_IMAGE)
  GlobalTimerExpired("Nobuffsyet","LOCALS")
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_MIRROR_IMAGE)
END

IF
  !StateCheck(Myself,STATE_BLUR)
  See(NearestEnemyOf(Myself))
  OR(2)
  !GlobalTimerNotExpired("Noillusions","LOCALS")
  GlobalTimerNotExpired("Spellimmunity","LOCALS")
  HaveSpell(WIZARD_BLUR)
  GlobalTimerExpired("Nobuffsyet","LOCALS")
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_BLUR)
END

IF
  See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  CheckStatGT(LastSeenBy(Myself),0,STONESKINS)
  HaveSpell(WIZARD_BREACH)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_BREACH)
END

IF
  See([ENEMY])
  InMyGroup(LastSeenBy(Myself))
  OR(4)
  StateCheck(LastSeenBy(Myself),STATE_SLEEPING)
  StateCheck(LastSeenBy(Myself),STATE_CHARMED)
  StateCheck(LastSeenBy(Myself),STATE_FEEBLEMINDED)
  StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  HaveSpell(WIZARD_DISPEL_MAGIC)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

IF
  See(NearestEnemyOf(Myself))
  !HasBounceEffects(LastSeenBy(Myself))
  HaveSpell(WIZARD_MAZE)
  !StateCheck(LastSeenBy(Myself),STATE_BERSERK)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_MAZE)
END

IF
  See(SecondNearestEnemyOf(Myself))
  !HasBounceEffects(LastSeenBy(Myself))
  HaveSpell(WIZARD_MAZE)
  !StateCheck(LastSeenBy(Myself),STATE_BERSERK)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_MAZE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_POWER_WORD_STUN)
  HPLT(LastSeenBy(Myself),90)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_POWER_WORD_STUN)
END

IF
  OR(3)
  See(Nearest([PC.0.0.CLERIC_ALL]))
  See(Nearest([PC.0.0.MAGE_ALL]))
  See(Nearest([PC.0.0.DRUID_ALL]))
  Allegiance(Myself,ENEMY)
  !HasBounceEffects(LastSeenBy(Myself))
  HaveSpell(WIZARD_POWER_WORD_SILENCE)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_POWER_WORD_SILENCE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_SLOW)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_SLOW)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_SPOOK)
  !StateCheck(LastSeenBy(Myself),STATE_BERSERK)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_SPOOK)
END

IF
  See(NearestEnemyOf(Myself))
  !HasBounceEffects(LastSeenBy(Myself))
  HaveSpell(WIZARD_OTILUKES_RESILIENT_SPHERE)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_OTILUKES_RESILIENT_SPHERE)
END

IF
  See([ENEMY])
  !InMyGroup(LastSeenBy(Myself))
  HaveSpell(WIZARD_FLAME_ARROW)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
  See([ENEMY])
  !InMyGroup(LastSeenBy(Myself))
  HaveSpell(WIZARD_MELF_ACID_ARROW)
THEN
  RESPONSE #100
    Spell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_GREASE)
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_GREASE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MAGIC_MISSILE)
  !HasItemEquiped("CLCK26",LastSeenBy(Myself))
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_CHROMATIC_ORB)
  !HasItemEquiped("CLCK26",LastSeenBy(Myself))
THEN
  RESPONSE #100
    Spell(NearestEnemyOf(Myself),WIZARD_CHROMATIC_ORB)
END

IF
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MELF_METEOR)
THEN
  RESPONSE #100
    Spell(Myself,WIZARD_MELF_METEOR)
END

IF
  See(Nearest([GOODCUTOFF]))
  HasItem("melfmet",Myself)
  Allegiance(Myself,ENEMY)
  ActionListEmpty()
  !HasBounceEffects(LastSeenBy(Myself))
THEN
  RESPONSE #100
    EquipItem("melfmet")
    AttackReevaluate(LastSeenBy(Myself),15)
END

IF
  See(NearestEnemyOf(Myself))
  !HasItem("melfmet",Myself)
  ActionListEmpty()
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackReevaluate(LastSeenBy(Myself),15)
END

